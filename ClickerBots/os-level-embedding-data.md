# Внедрение данных на уровне ОС

## Windows API

Главная задача любой ОС - это управление программными и аппаратными ресурсами компьютера, а также предоставление к ним доступа для запущенных процессов. Аппаратные ресурсы мы уже рассматривали. Это память, процессорное время, периферийные устройства. Примеры програмных ресурсов - примитивы синхронизации и алгоритмы, реализованные в системных библиотеках.

В этой книге мы будем рассматривать только ОС Windows. Под ней вы сможете запустить все приведённые примеры. В дальнейшем для простоты будем подразумевать Windows, когда говорим ОС.

Иллюстрация 2-1 демонстрирует, как ОС предоставляет доступ к своим ресурсам. Каждый запущенный процесс может обратиться к Windows для выполнения какого-то действия (например создание нового окна, отправки сетевого пакета, выделения дополнительной памяти и т.д.). Для каждого из этих действий у ОС есть соответствующая подрограмма. Подпрограммы, которые решают задачи из одной области (например работа с сетью), собраны в отдельные системные библиотеки. На иллюстрации приведены библиотеки *kernel32.dll*, *gdi32.dll* и другие.

![Windows API](windows-api.png)

**Иллюстрация 2-1.** *Доступ к ресурсам ОС через WinAPI*

Способ, которым процесс может вызвать системную подпрограмму, строго определён, хорошо задокументирован и остаётся неизменным для данной версии ОС. Такое взаимодействие можно сравнить с юридическим договором. Если процесс выполняет предварительные условия для вызова подпрограммы, ОС гарантирует указанный в документации результат. Такой договор называется **интерфейс прикладного программирования Windows** (Windwos API или WinAPI).

Программное обеспечение очень гибко и легко меняется, согласно возникающим требованиям. Например, каждое обновление Windows вносит изменения в некоторые детали реализации ОС (например какую-то системную библиотеку). Эти детали реализации связаны между собой (например одна библиотека вызывает подпрограммы другой). Таким образом, даже небольшое изменение может оказать значительное влияние на систему в целом. То же самое справедливо и для игрового приложения. Единственное, что позволяет програмному обеспечению работать в этом море  постоянных изменений - это надежные интерфейсы. Именно WinAPI гарантирует согласованное состояние системы и обеспечивает совместимость между новыми версиями ОС и приложения.

На иллюстрации 2-1 приведены два типа приложений. **Win32 приложение** взаимодействует с подмножеством системных библиотек через WinAPI интерфейс. Win32 - это историческое название которое возникло в первой 32-битной версии Windows (Windows NT). Библиотеки, доступные через WinAPI (также известные как **WinAPI библиотеки**), содержат **высокоуровневые подпрограммы**. Термин "высокоуровневый" означает, что подпрограмма оперирует сложными абстракциями, такими как окно, элемент управления, файл и т.д.

Второй тип приложений называется **нэйтив** (иногда переводится, как "родной"). Они взаимодействуют с более низкоуровневыми библиотеками Windows и ядром ОС через **нэйтив API** (Native API). Преимущество этих библиотек в том, что они становятся доступны на раннем этапе загрузки системы, когда многие функции ОС еще не работоспособны. Подпрограммы этих библиотек оперируют простыми абстракциями, такими как страница памяти, процесс, поток и т.д.

WinAPI библиотеки вызывают подпрограммы низкоуровневых библиотек. Такое подход позволяет создавать сложные абстракции из более простых. Низкоуровневые библиотеки в свою очередь вызывают функции ядра.

Драйвера предоставляют упрощенное представление устройств для системных библиотек. Это представление включает в себя набор подпрограмм, которые выполняют характерные для данного устройства действия. WinAPI и низкоуровневые библиотеки могут обратиться к этим подпрограммам через функции ядра.

**Слой аппаратной абстракции** (Hardware Abstraction Layer или HAL) - это модуль ядра ОС, который предоставляет универсальный доступ к различному аппаратному обеспечению. Главная задача HAL - облегчить портирование и сопровождение Windows на новых аппаратных платформах. Подпрограммы HAL используются ядром ОС и драйверами устройств.

## Симуляция нажатий клавиш

Теперь мы рассмотрим способы симуляции нажатий клавиш. Это наибоее простой метод контроля ботом игрового приложения.

### Нажатия клавиш в активном окне

Рассмотрим, какие возможности предлагает AutoIt для решения нашей задачи. В списке доступных функций (www.autoitscript.com/autoit3/docs/
functions.htm) есть функция `Send`. Мы воспользуемся ей в тестовом скрипте, который будет нажимать клавишу "a" в окне приложения Блокнот (Notepad).

Алгоритм работы нашего сркипта выглядит следующим образом:

1. Найти окно Блокнота среди всех открытых окон.

2. Переключится на него.

3. Симулировать нажатие клавиши "a".

Для поиска окна приложения мы воспользуемся функцией `WinGetHandle`. Её первый параметр является обязательным и может быть как заголовком окна, так и его классом. Функция возвращает **дескриптор** (handle) окна. Дескриптор - это структура данных, которая представляет некоторый ресурс или объект ОС. Большинство функций WinAPI оперируют этими структурами при работе с объектами.

Указывать класс окна при вызове функции `WinGetHandle` предпочтительнее. Всегда есть вероятность, что окна двух работающий приложений будут иметь одинаковые заголовки (например оба пустые).

Для чтения класса окна Блокнота необходимо выполнить следующие шаги:

1. Запустить приложение Au3Info. Вы можете найти его в каталоге установки AutoIt. В моем случае полный путь до приложения: `C:\Program Files (X86)\AutoIt3\Au3Info.exe`.

2. Перетащить иконку "Finder Tool" на окно Блокнота и отпустить.

3. Вы увидите результат, как на иллюстрации 2-2.

![Приложение AutoIt3 Info](au3info.png)

**Иллюстрация 2-2.** *Приложение AutoIt3 Info*

Класса окна Блокнота отображается на панели "Basic Info Window". Этот класс - "Notepad".

Скрипт `Send.au3`, представленный в блоке кода 2-1, реализует описанный алгоритм симуляции нажатия клавиши "a".

**Блок кода 2-1.** *Скрипт `Send.au3`*
```AutoIt
$hWnd = WinGetHandle("[CLASS:Notepad]")
WinActivate($hWnd)
Send("a")
```
В первой строчке мы получаем дескриптор окна Блокнота с помощью функции `WinGetHandle`. Далее мы переключаем фокус ввода на это окно функцией `WinActivate`. Последнее действие - симуляция нажатия клавиши "a".

Чтобы запустить этот скрипт, создайте в вашем редакторе исходного кода файл с именем `Send.au3` и скопируйте в него приведенный выше код. После этого запустите скрипт двойным щелчком по иконке этого файла.

### `Send` функция

Функция `Send` представляет собой обертку над низкоуровневой подпрограммой WinAPI. Мы можем найти эту подпрограмму с помощью приложения API Monitor, которое перехватит все системные вызовы скрипта `Send.au3`.

Для подключения API Monitor к работающему процессу надо выполнить следующие шаги:

1. Запустить 32-битную версию API Monitor.

2. Переключится на панель "API Filter" щелчком мыши. Нажать комбинацию клавиш *Ctrl+F* - откроется диалог поиска. Ввести в поле "Find what:" текст “Keyboard and Mouse Input” и нажать кнопку "Find Next". Закрыть диалог поиска и активировать найденный флажок (check box) “Keyboard and Mouse Input”. 

3. Нажать *Ctrl+M* для открытия диалога "Monitor New Process". Выбрать приложение `AutoIt3.exe` в поле "Process" и нажать кнопку "OK". Путь по умолчанию до этого приложения: `C:\Program Files (x86)\AutoIt3\AutoIt3.exe`.

4. В открывшемся диалоге "Run Script" выбрать скрипт `Send.au3`. Сразу после этого скрипт отработает.

5. Переключиться на панель "Summary" окна API Monitor. С помощью диалога поиска, который вызывается по *Ctrl+F*, найти текст 'a' (с одинарными кавычками).

Иллюстрация 2-3 демонстрирует ожидаемый результат. Согласно перехваченным вызовам, `VkKeyScanW` - единственная WinAPI функция, получающая в качестве параметра символ "a". Если мы обратимся к официальной документации WinAPI (docs.microsoft.com/en-us/windows/desktop/apiindex/windows-api-list), выяснится что эта функция не выполняет нажатия клиавиши. Она вместе с функцией `MapVirtualKeyW` только подготавливает параметры для вызова `SendInput`, который и симулирует нажатие.

![Приложение API Monitor](api-monitor.png)

**Иллюстрация 2-3.** *Перехват вызовов WinAPI с помощью API Monitor*

Мы узнали достаточно, чтобы симулировать нажатие клавиши "a" напрямую через WinAPI вызовы. Удалим третью строчку скрипта `Send.au3` и заменим её новым блоком кода. При этом оставим первые два вызова `WinGetHandle` и `WinActivate` без изменений.

Блок кода 2-2 демонстрирует измененный скрипт.

**Блок кода 2-2.** *Скрипт `SendInput.au3`*
```AutoIt
$hWnd = WinGetHandle("[CLASS:Notepad]")
WinActivate($hWnd)

Const $KEYEVENTF_UNICODE = 4
Const $INPUT_KEYBOARD = 1
Const $iInputSize = 28

Const $tagKEYBDINPUT = _
    'word wVk;' & _
    'word wScan;' & _
    'dword dwFlags;' & _
    'dword time;' & _
    'ulong_ptr dwExtraInfo'

Const $tagINPUT = _
    'dword type;' & _
    $tagKEYBDINPUT & _
    ';dword pad;'

$tINPUTs = DllStructCreate($tagINPUT)
$pINPUTs = DllStructGetPtr($tINPUTs)
$iINPUTs = 1
$Key = AscW('a')

DllStructSetData($tINPUTs, 1, $INPUT_KEYBOARD)
DllStructSetData($tINPUTs, 3, $Key)
DllStructSetData($tINPUTs, 4, $KEYEVENTF_UNICODE)

DllCall('user32.dll', 'uint', 'SendInput', 'uint', $iINPUTs, _
        'ptr', $pINPUTs, 'int', $iInputSize)
```

Для вызова `SendInput` мы использовали функцию AutoIt `DllCall`. Эта функция вызывает из **динамческой библиотеки** (DLL) подпрограмму, написанную на языке C или C++. Её входные параметры должны иметь типы, согласно документации WinAPI. Некоторые из этих типов AutoIt не поддерживает на уровне синтаксиса. Поэтому нам нужны дополнительные функции, чтобы подготовить входные параметры для `SendInput`.

Рассмотрим, какие параметры мы передали в функцию `DllCall`:

* `user32.dll` - имя библиотеки, подпрограмму которой требуется вызвать.

* `uint` - это тип возвращаемого значения подпрограммы.

* `SendInput` - её имя.

* `uint, $iINPUTs, ptr, $pINPUTs, int, $iInputSize` - пары тип-переменная, которые являются входными параметрами подпрограммы.

Согласно WinAPI документации, декларация функции `SendInput` выглядит следующим образом:
```C++
UINT SendInput(UINT cInputs, LPINPUT pInputs, int cbSize);
```
Строчку вызова функции `DllCall` на AutoIt можно представить следующим эквивалентом на языке C++:
```C++
SendInput(iINPUTs, pINPUTs, iInputSize);
```
Рассмотрим входные параметры, переданные нами в `SendInput`:

1. `iINPUTs` - количество структур типа `INPUT`, которые передаются вторым параметром. В нашем случае это количество равно единице.

2. `pINPUTs` - указатель на массив структур типа `INPUT` из одного элемента. Этот массив подготавливается в несколько этапов. Сначала мы объявляем AutoIt переменные, описывающие структуры `KEYBDINPUT` и `INPUT`. Структура `KEYBDINPUT` нужна, поскольку является одним из полей `INPUT`. Такое отношение называется **вложенные структуры**. Следующий шаг - создание C++ структуры через вызов `DllStructCreate`. Результат сохраняется в переменной `tINPUTs`. С помощью функции `DllStructGetPtr` мы получаем указатель на эту структуру и помещаем его в `pINPUTs`. Когда структура создана, мы записываем её поля через `DllStructSetData` вызовы. Обратите внимание, что второй параметр `DllStructSetData` - это номер поля структуры начиная с единицы. В случае вложенных структур нумерация продолжается. То есть элемент 1 соответствует полю `dword type` структуры `INPUT`, а элемент 3 - полю `word wScan` структуры `KEYBDINPUT`.

3. `iInputSize` - размер в байтах одной структуры `INPUT`. В нашем случае это константное значение, рассчитанное по формуле:
```
dword + (word + word + dword + dword + ulong_ptr) + dword =
4 + (2 + 2 + 4 + 4 + 8) + 4 = 28
```
Слагаемые в скобочках - это размеры полей вложенной структуры `KEYBDINPUT`.





